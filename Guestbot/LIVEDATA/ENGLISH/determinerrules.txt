
############################################
########### Rules about pre determiners
############################################

# can only be before a or an or the
0	INCLUDE * PREDETERMINER
	!HASPROPERTY PREDETERMINER_TARGET  
DISCARD

# must be before a , an, the
0	INCLUDE * PREDETERMINER
	HASPROPERTY PREDETERMINER_TARGET 	 
KEEP

############################################
########### Rules about determiners
############################################

# determiner cannot precede aux verb		"*what will tomorrow bring?"
0	INCLUDE * DETERMINER
	IS AUX_VERB_BITS 
DISCARD

# determiners wont proceed potential aux verbs that are not potential nouns like can
0	INCLUDE *  DETERMINER
	HAS AUX_VERB_BITS
	STAY !HAS NOUN_BITS
DISCARD

# determiners cant preceed aux-verb or real verb or pronoun or another determiner
0	INCLUDE * DETERMINER_BITS
	IS DETERMINER_BITS AUX_VERB_BITS VERB_TENSES PRONOUN_BITS POSSESSIVE_BITS # can precede preposition:  These out of the way places
DISCARD

# determiner cannot be after a determiner or an adjective
-1	IS DETERMINER_BITS ADJECTIVE_BITS
	INCLUDE * DETERMINER_BITS
DISCARD

# determiner cannot preceed an adverb not preceeding an adjective possessive pronoun or noun
0	INCLUDE  * DETERMINER_BITS
	IS ADVERB_BITS
	!HAS ADVERB_BITS ADJECTIVE_BITS NOUN_BITS  - NOUN_GERUND PRONOUN_POSSESSIVE
DISCARD

# determiner cannot end the sentence
0 	INCLUDE  * DETERMINER_BITS  
	STAY END  
DISCARD 	

# determiner not before a noun isnt possible (skip past adjective + adverbs and prep phrase describing the noun)
0 INCLUDE  * DETERMINER_BITS
	SKIP IS ADVERB_BITS ADJECTIVE_BITS 
	!HAS NOUN_BITS
DISCARD 

# determiner wont intervene between two nouns,  "this is the house *that Jack built"  except that "I give the dog a bone" does, as does "he goes to school each semester" does

# determiners do not come before pronouns - illegal  they were *certain they were happy
0	INCLUDE * DETERMINER
	IS PRONOUN_BITS
DISCARD

# determiner not before a possible noun  "had I been brave *enough"
0 INCLUDE * DETERMINER
	SKIP !HAS NOUN_BITS
	!HAS NOUN_BITS
DISCARD

# a potential determiner which is also an adjective, cannot be preceeded by an adjective or determiner or possessive
-1 IS DETERMINER ADJECTIVE_BITS PRONOUN_POSSESSIVE POSSESSIVE
	INCLUDE * DETERMINER # after his double teeth
	STAY HAS ADJECTIVE_BITS
DISCARD

# these not after pronoun subject (except if question inversion happens)
-2	!HAS AUX_VERB_BITS
	IS PRONOUN_SUBJECT
	INCLUDE *  DETERMINER_BITS  # adjective allowed: Are you sick?
DISCARD

#  determiner/noun plurality fail - ILLEGAL: This reports  - LEGAL: the people
0	INCLUDE * DETERMINER
	STAY HASPROPERTY OTHER_SINGULAR  # noncanonical
	IS NOUN_PLURAL
DISCARD

# potential determiner vs pronouns followed by potential noun followed by end of sentence will be determiner "I would sooner read a book than watch *this film" but not "we watched *them play"
0	INCLUDE * DETERMINER
	STAY HAS PRONOUN_BITS
	HAS NOUN_BITS
	STAY !HAS  NOUN_GERUND NOUN_INFINITIVE
	STAY END
KEEP

# possessive pronoun that is also a determiner "my dog" make it a determiner (will preceed all other things)
0	INCLUDE * DETERMINER 
	STAY HAS PRONOUN_POSSESSIVE
KEEP

# a potential determiner which is also an adjective not preceeded by an adjective or determiner  or possessive
-1 !HAS DETERMINER ADJECTIVE_BITS  PRONOUN_POSSESSIVE POSSESSIVE
	INCLUDE * DETERMINER 
	STAY HAS ADJECTIVE_BITS
	SKIP !HAS NOUN_BITS
	HAS NOUN_BITS
KEEP

# determiner before a noun but not after a noun (skip past adjective + adverbs and prep phrase describing the noun) will be a determiner
-1	!HAS NOUN_BITS
	INCLUDE  * DETERMINER_BITS
	SKIP IS ADVERB_BITS ADJECTIVE_BITS 
	IS NOUN_BITS
KEEP 
