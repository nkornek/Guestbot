
############################################
########### Rules about pronoun possessive
############################################

# possessive pronoun cannot follow an adjective or (must preceed it or a noun or an adverb)
-1 	IS ADJECTIVE_BITS 			# 
	INCLUDE  *  PRONOUN_POSSESSIVE
DISCARD  

# these not after pronoun subject (except if question inversion happens)
-2	!HAS AUX_VERB_BITS
	IS PRONOUN_SUBJECT
	INCLUDE *   PRONOUN_POSSESSIVE  # adjective allowed: Are you sick?
DISCARD

# cant have possessive in front of various things
0	INCLUDE * PRONOUN_POSSESSIVE
	IS COMMA PAREN PREPOSITION CONJUNCTION_COORDINATE CONJUNCTION_SUBORDINATE VERB_TENSES AUX_VERB_BITS TO_INFINITIVE 
DISCARD

# possessive cannot precede determiner or preposition or conjunction or comma or paren
0  INCLUDE * PRONOUN_POSSESSIVE
	IS DETERMINER PREPOSITION CONJUNCTION_SUBORDINATE CONJUNCTION_COORDINATE COMMA PAREN
DISCARD

# pronoun posssive ending the sentence cannot exist, but "it was bob's" can
0	INCLUDE * PRONOUN_POSSESSIVE
	STAY END
DISCARD

# these not after regular nouns
-1	IS NOUN_BITS  - NOUN_GERUND
	INCLUDE *   PRONOUN_POSSESSIVE 
DISCARD

# pronouns cannot be adjcant to each other 
0	INCLUDE *  PRONOUN_POSSESSIVE 
	IS PRONOUN_BITS POSSESSIVE_BITS 
DISCARD

# pronouns cannot be adjcant to each other 
-1 IS PRONOUN_BITS POSSESSIVE_BITS 
	INCLUDE *  PRONOUN_POSSESSIVE 
DISCARD

# for now, assume if possessive has potential noun after it, be possessive "*his home was" but not "I saw *her light the light"
0	INCLUDE * PRONOUN_POSSESSIVE
	HAS NOUN_BITS - NOUN_INFINITIVE - NOUN_GERUND
	STAY !HAS NOUN_INFINITIVE
KEEP

# possessive after potential aux is just that - "he did his fart"  vs "the field was his" or "his was the real issue"
-1  HAS AUX_VERB_BITS
	INCLUDE * PRONOUN_POSSESSIVE
	STAY !END  # but the field was his to
	!HAS TO_INFINITIVE
KEEP

# cannot have object pronoun before a noun or adjective so must be possessive if it can be -- but will fail with clauses
0	INCLUDE  * PRONOUN_POSSESSIVE  # The  designer  window  treatments  in  (her)  house  ,  installed  17  years_ago , were outmoded 
	IS NOUN_BITS ADJECTIVE_BITS
KEEP 

# possessive will follow preposition  "to his house"
-1 IS PREPOSITION
	INCLUDE * PRONOUN_POSSESSIVE
KEEP

############################################
########### Rules about pronoun  
############################################
# pronouns cannot be adjacent to each other - but they can:   what they found intriuged them

# potential pronoun in front of stuff cannot be a clause starter pronoun if verb  cant be "*what they found thrilled them"  (relative pronoun) . But can be at end: "you should do *that"

# these not after a determiner
-1	IS DETERMINER
	INCLUDE * PRONOUN_BITS 
DISCARD

# these not after pronoun subject (except if question inversion happens)
-2	!HAS AUX_VERB_BITS
	IS PRONOUN_SUBJECT
	INCLUDE * PRONOUN_BITS   # adjective allowed: Are you sick?
DISCARD

# pronoun possessive/object her cannot be object before a singular noun - subject/object before noun can ONLY happen on clause/phrase change - near her angels sing
0	INCLUDE * PRONOUN_OBJECT PRONOUN_SUBJECT
	IS NOUN_SINGULAR  # in *her home  vs   After biting *her cats ran away
DISCARD

# there cannot be followed by AUX, only real 
0	INCLUDE * PRONOUN_BITS
	STAY ISORIGINAL there # all of the cars *there are going home. (VERY SPECIFIC)
	IS AUX_VERB_BITS
DISCARD

# if choosing determiner vs pronoun of some flavor, followed by a noun, drop pronoun
0	INCLUDE * PRONOUN_BITS   # There were 130 contacts *this month. 
	STAY  HAS DETERMINER_BITS
	IS NOUN_BITS - NOUN_GERUND
DISCARD

# clause start potential pronoun in front  of verb "*what they found thrilled them"  (relative pronoun) " I want the one that dangled from the towel"
0	INCLUDE * PRONOUN_BITS
	STAY HASPROPERTY POTENTIAL_CLAUSE_STARTER
	IS VERB_TENSES
KEEP

# What about -- becomes pronoun and preposition
0	ISORIGINAL what
	STAY INCLUDE * PRONOUN_BITS
	ISORIGINAL about
KEEP 

# cannot have a subject pronoun before an adjective[ unless after main verb in inverted format] (implying a pronoun before a noun), except object of clause before next clause subject
# legal: who are *you afraid of

# a potential pronoun with verb/aux following it is a good thing
0	INCLUDE * PRONOUN_BITS
	STAY HAS CONJUNCTION_SUBORDINATE	#  "that" which could be either - the team *that wore red
	IS VERB_TENSES AUX_VERB_BITS # LEGAL: after being reprimaned, he went home. UNKNOWN: after walking home, he left the city. is walking participle or gerund
KEEP

############################################
########### Rules about pronoun  subject/object
############################################

# subject object after start of sentence question will be subject so discard object
-1	IS AUX_VERB_BITS
	INCLUDE * PRONOUN_OBJECT
	STAY CANONLYBE PRONOUN_SUBJECT PRONOUN_OBJECT
DISCARD

# subject object conflict after preposition or regular verb will be object  "he lookd for *you"  and "I hit *you" except question form like "are *you a bank teller"
-1  IS PREPOSITION VERB_TENSES
	STAY !START
	INCLUDE * PRONOUN_SUBJECT
	STAY HAS PRONOUN_OBJECT
DISCARD

# subject object conflict after be verb start will be object  "he lookd for *you"  and "I hit *you" except question form like "are *you a bank teller"
-1  IS  VERB_TENSES
	STAY START
	INCLUDE * PRONOUN_OBJECT
	STAY HAS PRONOUN_SUBJECT
DISCARD

# object not after regular nouns or subordinate conjunctions "after *you wept"
-1	IS NOUN_BITS  - NOUN_GERUND CONJUNCTION_SUBORDINATE
	INCLUDE *  PRONOUN_OBJECT  
DISCARD

# Pronoun Object cannot start a sentence unless its a q word  eg "what is the key"
0	INCLUDE  * PRONOUN_OBJECT		# (Her) fashion sense is good.
	STAY START
	!ISQUESTION qword
DISCARD

# pronoun following an aux will be subject form - "what have *you been doing"
-1	IS AUX_VERB_BITS
	INCLUDE * PRONOUN_SUBJECT
KEEP

# pronoun before verb or aux should be subject, not object BUT not if ending a clause-- "how you do it is your business"
		# I can light the fire and *you can open the door.

# pronoun after verb should be object, not subject (indirect object) # I hit you the ball, but: After he walked I ran 

#  conflict pronoun followed by preposition will be the pronoun - "*all of the cars"  EXCEPT "I got *here on my own"
0	INCLUDE * PRONOUN_SUBJECT PRONOUN_OBJECT
	IS PREPOSITION
KEEP

