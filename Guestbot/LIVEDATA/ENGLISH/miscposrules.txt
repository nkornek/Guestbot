

############################################
########### Rules about more
############################################

# more is adverb in more xxx than phrase: he is *more tall than her
0 INCLUDE * ADVERB_BITS	
  STAY ISORIGINAL more
  HAS NOUN_BITS VERB_TENSES ADJECTIVE_BITS
  ISORIGINAL than
KEEP

############################################
########### Rules about existential there
############################################

# these not after a determiner or regular nouns
-1	IS DETERMINER NOUN_BITS  - NOUN_GERUND
	INCLUDE *     THERE_EXISTENTIAL
DISCARD

# these not after pronoun subject (except if question inversion happens)
-2	!HAS AUX_VERB_BITS
	IS PRONOUN_SUBJECT
	INCLUDE *     THERE_EXISTENTIAL  # adjective allowed: Are you sick?
DISCARD

# there NOT followed by "be" will not be existential "there is"  "there would often be"
0	INCLUDE * THERE_EXISTENTIAL
	SKIP !HAS VERB_TENSES
	!ISCANONICAL be
DISCARD

# existential there question "is *there a future in it"
-1 	START 			  
	STAY ISCANONICAL be 
	INCLUDE  * THERE_EXISTENTIAL 
KEEP 	

# existential there statement at start-- "there seems to be fun everywhere"
0 	INCLUDE  * THERE_EXISTENTIAL 			 
	STAY START
	HASCANONICALPROPERTY EXISTENTIAL_BE 
KEEP 

# existential there statement
0 	INCLUDE  * THERE_EXISTENTIAL 			# (There) is a future in it.  
	HASCANONICALPROPERTY EXISTENTIAL_BE 
	IS DETERMINER ADJECTIVE_BITS NOUN_BITS - NOUN_GERUND
KEEP 	

# There  w/o be or seem ... after is not existential
0	INCLUDE  * THERE_EXISTENTIAL 
	!HASCANONICALPROPERTY EXISTENTIAL_BE 
DISCARD 

############################################
########### Rules about To infinitive
############################################

#  "he has to have been the first student *to try that"  but not he went to bed

# "My job is *to search"  vs I went to bed


# if sentence starts with qword && is ending with prep/noun or to/verb, presume to/verb goes with the object qword as a verbal  "what do you like *to do"
0 INCLUDE * TO_INFINITIVE
	STAY ISQUESTION qword
	HAS NOUN_INFINITIVE VERB_INFINITIVE VERB_INFINITIVE
	STAY END
KEEP

# cannot end a sentence - "This is the man I wanted to speak *to"
0 INCLUDE * TO_INFINITIVE
	STAY END
DISCARD

# we assume a to/prep conflict followed by a possible nouninfintive and then a noun will become nounfininitve "I saw George who continued *to play videogames"
0	INCLUDE * TO_INFINITIVE
	HAS NOUN_INFINITIVE VERB_INFINITIVE
	IS NOUN_BITS
KEEP

# these not after pronoun subject (except if question inversion happens)
-2	!HAS AUX_VERB_BITS
	IS PRONOUN_SUBJECT
	INCLUDE *      TO_INFINITIVE # adjective allowed: Are you sick?
DISCARD

# these not after a determiner
-1	IS DETERMINER
	INCLUDE *      TO_INFINITIVE 
DISCARD

# TO at end of sentence cant be infinitive
0 INCLUDE  * TO_INFINITIVE
	STAY END
DISCARD

# lose TO-infinitive if the next word is not a noun infinitive or an adverb - "I went *to my home" but not: "I want *to boldly go"  
0	INCLUDE  * TO_INFINITIVE #   This is the way (to) my home.
	!HAS NOUN_INFINITIVE  VERB_INFINITIVE ADVERB_BITS
DISCARD 

# set to-infinitive before NOUN_INFINTIVE
0	INCLUDE  * TO_INFINITIVE
	SKIP IS ADVERB_BITS
	IS NOUN_INFINITIVE   VERB_INFINITIVE
 KEEP 

############################################
########### Rules about commas
############################################

# determiner and conjunction and preposition and cannot preceed comma
0	INCLUDE * DETERMINER_BITS CONJUNCTION_SUBORDINATE CONJUNCTION_COORDINATE PREPOSITION  # but to-infintive can:  I like to, as they say, go home.
	IS COMMA
DISCARD

############################################
########### Rules on combinations
############################################


# potential relative adverb in front of noun or pronoun could be a clause starter adverb or conjunction if it can be "*what they found thrilled them"  (relative adverb) or "I saw him when he ate dinner"
0	INCLUDE * ADVERB_BITS CONJUNCTION_SUBORDINATE DETERMINER PRONOUN_BITS # or "I like *what I ate"  as object of clause
	STAY HASPROPERTY POTENTIAL_CLAUSE_STARTER
	IS PRONOUN_BITS ADJECTIVE_BITS NOUN_BITS DETERMINER PRONOUN_POSSESSIVE
KEEP

# conjoined potential noun or verb and pronoun - cant conjoin adjective and pronoun - false: Legal::  The can is open and we can eat 
# potential adjective or adverb or noun following aux be (not a verb)	# They were (outmoded). But... the men who were *dressed in red ate first

# potential noun/adjective/participle following aux be, but adverb and not followed by potential adjective or adverb - but not "been being"
-1	ISCANONICAL be # 	# LEGAL: There is nothing here.  All cars were going to the store. ILLEGAL: They were now certain. 
	STAY IS AUX_VERB_BITS
	INCLUDE *  NOUN_BITS ADJECTIVE_BITS VERB_PRESENT_PARTICIPLE  VERB_PAST_PARTICIPLE
	STAY !ISORIGINAL being
	STAY HAS ALL - NOUN_BITS - ADJECTIVE_BITS - VERB_PRESENT_PARTICIPLE  - VERB_PAST_PARTICIPLE
	STAY !HAS ADVERB_BITS
	!HAS ADJECTIVE_BITS ADVERB_BITS 
KEEP  
