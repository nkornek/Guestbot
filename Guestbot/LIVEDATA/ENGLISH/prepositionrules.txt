
############################################
########### Rules about prepositions
############################################

# prep-to conflict followed by NOUN_INFINITIVE will be to  "I employ you *to fight"
0	INCLUDE * PREPOSITION
	STAY HAS TO_INFINITIVE
	IS NOUN_INFINITIVE
DISCARD

# to xxx is a prep when not followed by INFINITIVE or adverb separating infinitive "to boldly go"
0	INCLUDE * TO_INFINITIVE
	STAY HAS PREPOSITION
	!HAS VERB_INFINITIVE NOUN_INFINITIVE ADVERB_BITS
DISCARD

# in conflict between prep and adverb for "but" after is and before adjective be adverb "there is *but one god"
-1	ISCANONICAL be
	INCLUDE * PREPOSITION
	STAY ISORIGINAL but
	HAS ADJECTIVE_BITS NOUN_BITS PRONOUN_SUBJECT PRONOUN_OBJECT DETERMINER
DISCARD

# 20 *like the product

# if adverb prep conflict and followed by object, prefer prep "he went to the sink all *by himself"
0	INCLUDE * PREPOSITION
	STAY CANONLYBE PREPOSITION ADVERB_BITS
	IS DETERMINER ADJECTIVE_BITS NOUN_BITS PRONOUN_BITS
KEEP

# preposition must be followed by noun phrase or its particle or end of sentence wrapped
0	INCLUDE * PREPOSITION
	STAY !END
	SKIP IS ADVERB_BITS
	!IS ADJECTIVE_BITS PRONOUN_BITS POSSESSIVE_BITS NOUN_BITS DETERMINER_BITS
DISCARD

# these not after a determiner or preposition
REVERSE 0 	INCLUDE *  PREPOSITION 
	SKIP IS ADJECTIVE_BITS
	IS DETERMINER PREPOSITION
DISCARD

# prep adj prep is not possible nor is *prep prep "she was *as famous for her dancing as for her love"
0	INCLUDE * PREPOSITION	
	SKIP IS ADJECTIVE_BITS
	IS PREPOSITION  CONJUNCTION_COORDINATE  CONJUNCTION_SUBORDINATE
DISCARD

# preposition w/o appropriate potential nouniness or pronoun after cant be, except at end of sentence 
0 INCLUDE * PREPOSITION
	STAY !END
	!HAS NOUN_DESCRIPTION_BITS PRONOUN_BITS TO_INFINITIVE
DISCARD

# preposition cannot preceed or pronoun subject or an aux verb or a regular verb - but could preceed pronoun subject of a clause: the visitor for whom we waited was.
0	INCLUDE * PREPOSITION
	IS  AUX_VERB_BITS VERB_TENSES 	# After *he left I stayed
DISCARD

# normally prepositions are not at end of sentence (though looping back to front happens with "whom are you thinking of"). But if can be adverb, be that unless start is qword
0 INCLUDE * PREPOSITION
	STAY END
	STAY HAS ADVERB_BITS
	!ISQUESTION qword
DISCARD

# prep with no noun after it wont be prep "I want to acquaint with"  unless at end of sentence wrapping to front
0	INCLUDE * PREPOSITION
	STAY !END
	STAY NONEAFTER NOUN_BITS PRONOUN_SUBJECT PRONOUN_OBJECT
DISCARD

# terminal prep with no subject start wont be prep "where are you from?" "what do you eat with"
0	INCLUDE * PREPOSITION
	STAY END
	!ISQUESTION qword
DISCARD

# I like a steak -- not  a prep... he hit the wall like x ---  like in 2nd or 3rd position is NOT a prep  --- but maybe "Boys like John are evil"  not handled right
-1	START
	INCLUDE * PREPOSITION
	STAY ISORIGINAL like
DISCARD

# I like a steak -- not  a prep... he hit the wall like x ---  like in 2nd or 3rd position is NOT a prep
-2	START
	HAS ADVERB_BITS ADJECTIVE_BITS NOUN_BITS DETERMINER PREDETERMINER VERB_TENSES AUX_VERB_BITS PREPOSITION PRONOUN_BITS CONJUNCTION_SUBORDINATE 
	INCLUDE * PREPOSITION
	STAY ISORIGINAL like
DISCARD


########################
# ordinary preposition
########################

# prep /conjunc before noun-gerund will be one of those instead of anything else - "I remembered to eat *after walking home"
0	INCLUDE * PREPOSITION CONJUNCTION_SUBORDINATE
	STAY HAS TAG_TEST - PREPOSITION - CONJUNCTION_SUBORDINATE
	IS NOUN_GERUND
KEEP

# prep if start wraps to Qword - but not "from where do you *come"

# prep that cant be conjunction or adverb with adjective after it will be prep  but not adverb "for *over 10 years"
0	INCLUDE * PREPOSITION
	STAY !HAS CONJUNCTION_SUBORDINATE ADVERB_BITS
	IS  ADJECTIVE_BITS  
KEEP

# prep that cant be conjunction or adverb or noun-infinitive with determiner/noun after it will be prep "you had better clean *up your room" - except as adverb "for *over 10 years" "he showed John how to *cross his legs"
0	INCLUDE * PREPOSITION
	STAY !HAS CONJUNCTION_SUBORDINATE NOUN_INFINITIVE
	IS DETERMINER POSSESSIVE_BITS NOUN_BITS
KEEP

# What about -- becomes pronoun and preposition
-1	ISORIGINAL what
	ISORIGINAL about
	STAY INCLUDE * PREPOSITION
KEEP 

# prep followed by signs of a noun should be prep (except verb LIKE)  "a tomb built *by man"  but not: "*after the rock was carved"  but not 2 possible preps in a row (one is adverb?) "he swam out over 5 feet
 -1	!HAS PREPOSITION
 	INCLUDE * PREPOSITION
	STAY !HAS VERB_TENSES CONJUNCTION_SUBORDINATE TO_INFINITIVE 
	HAS DETERMINER PREDETERMINER ADJECTIVE_BITS NOUN_BITS 
KEEP

# noun at end of sentence preceeded by preposition MUST be a noun  "a tomb built by *man"
-1	IS PREPOSITION
	INCLUDE * NOUN_BITS
	STAY END
KEEP

# if this might be prep and subsequent is possessive noun but not a conjunction prep, make this a prep 
0	INCLUDE  * PREPOSITION
	STAY !HAS CONJUNCTION_BITS
	IS NOUN_BITS
	IS POSSESSIVE
KEEP 

# 3rd person possessive noun prep phrase
0	INCLUDE  * PREPOSITION # We all went over to John's house
	IS NOUN_BITS
	IS POSSESSIVE
	IS NOUN_BITS
KEEP

########################
# "as" as a preposition
########################

# as is a preposition followed by a noun or phrase "*as a boy"
0	INCLUDE * PREPOSITION
	STAY ISORIGINAL as
	IS DETERMINER
KEEP

# as is a preposition followed by a noun or phrase "*as a young boy"
0	INCLUDE * PREPOSITION
	STAY ISORIGINAL as
	IS ADJECTIVE_BITS
	IS NOUN_BITS
KEEP

############################################
########### Rules about particles
############################################

# particles are a kind of preposition, but can move after the object, are idiomatic expressions that may only exist with some objects "run up the bill" vs "run up the hill"

# Particle fails to find a matching particle verb
REVERSE	0 INCLUDE  * PARTICLE
	SKIP !HAS VERB_TENSES NOUN_GERUND ADJECTIVE_PARTICIPLE # has some kind of verb before it
	STAY !HASCANONICALPROPERTY SEPARABLE_PHRASAL_VERB 
	STAY IS VERB_TENSES NOUN_GERUND ADJECTIVE_PARTICIPLE 
DISCARD

# Particle finds a matching particle verb but looks like a prep phrase, leave it as prep phrase
REVERSE	-1	IS NOUN_BITS PRONOUN_OBJECT DETERMINER PREDETERMINER ADJECTIVE_BITS	# not adverb because we wouldnt know 
	INCLUDE  * PARTICLE
	SKIP !IS VERB_TENSES  NOUN_GERUND ADJECTIVE_PARTICIPLE # has some kind of verb before it
	STAY HASCANONICALPROPERTY SEPARABLE_PHRASAL_VERB 
	STAY IS VERB_TENSES NOUN_GERUND ADJECTIVE_PARTICIPLE 
DISCARD

# terminal particle discard if could be adverb - BUT not "I will take the day *off"

# particle can never START a sentece
0	INCLUDE * PARTICLE
	STAY START
DISCARD

# these not after a determiner or preposition
-1	IS DETERMINER PREPOSITION
	INCLUDE * PARTICLE 
DISCARD

# these not after pronoun subject (except if question inversion happens)
-2	!HAS AUX_VERB_BITS
	IS PRONOUN_SUBJECT
	INCLUDE *  PARTICLE  # adjective allowed: Are you sick?
DISCARD

# potential particle directly following a particle verb should be such as long as not a preposition -- "the boy stood *up quickly and walked away"
-1	IS VERB_TENSES NOUN_GERUND ADJECTIVE_PARTICIPLE 
	STAY HASCANONICALPROPERTY SEPARABLE_PHRASAL_VERB 
	INCLUDE * PARTICLE
	SKIP !HAS ADVERB_BITS 
	HAS COMMA CONJUNCTION_BITS  # not going to find an object if it were a preposition
 KEEP

# terminal particle accept if not adverb
REVERSE 0 INCLUDE * PARTICLE
	STAY END
	STAY !HAS ADVERB_BITS
	SKIP !HASCANONICALPROPERTY SEPARABLE_PHRASAL_VERB   # has some particle verb before it (may not belong to it)
	HASCANONICALPROPERTY SEPARABLE_PHRASAL_VERB 
KEEP

# Particle finds a matching particle verb and doesnt look like a prep phrase because has one after it
REVERSE	-1	IS  PREPOSITION 
	INCLUDE  * PARTICLE
	SKIP !HASCANONICALPROPERTY SEPARABLE_PHRASAL_VERB # has some kind of particle verb before it (may not belong to it)
	HASCANONICALPROPERTY SEPARABLE_PHRASAL_VERB 
KEEP

# particle immediately after particle verb and junk after that "the thought of getting up and running is bad"
-1 HASCANONICALPROPERTY SEPARABLE_PHRASAL_VERB 
	INCLUDE * PARTICLE
	IS PAREN COMMA CONJUNCTION_BITS
KEEP