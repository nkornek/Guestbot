concept: ~murderplot (~plot plan plan~3 assassination murder scheme machination intrigue conspiracy cabal kill murder anxiety stress circumstances)
concept: ~evilqueen (queen "evil queen" stepmother she stepmom)
concept: ~agreement ("I know" "I agree" "I understand" "totally" "totes")

topic: ~assassination_plot (~murderplot ~evilqueen)

#! Possible related pronouns
u: (_she) ^mark(queen _0)  ^repeat() ^keep() 
u: (_her) ^mark(queen _0)  ^repeat() ^keep() 
u: (_it) ^mark(plot _0)  ^repeat() ^keep() 

u: ($end==1) The door is shut, no one is hearing you. ^keep() ^repeat()

#! ----------------------------------------------------------------
#! fishing for more info
#! ----------------------------------------------------------------

?: ($stepmother!=1 [(how * ~murderplot) (what * ~evilqueen * give)]) First it was bodice lace that nearly squeezed the death out of me. Then it was a poisoned comb. Can you imagine such a thing?
	a: (~YES) You read too much gothic fiction.
	a: (~NO) She's out of her mind.

?: ($stepmother!=1 << why ~evilqueen ~murderplot >>) Apparently she's jealous of my beauty and wants to kill me for that!
	a:([silly ridiculous ludicrous preposterous bizarre absurd preposterous unbelievable grotesque farcical]) Isn't it?!
		$trust+=1

?: ($stepmother!=1 who * ~evilqueen) Simply the kingdom's queen!

?: ($stepmother!=1 [what who] * ~murderplot) My stempother wants to kill me so that she can claim to be the most beautiful living woman.

?: ($stepmother!=1 ~evilqueen * ~murderplot * you) Yes, she's evil and is jealous of my beauty!

?: ($stepmother!=1 stress~3) I have to sleep with one eye open since my stepmother keeps trying to kill me!
	$plot = 1

#! ----------------------------------------------------------------
#! Compassion move
#! ----------------------------------------------------------------

s: ( << [that it situation ~evilqueen ~murderplot] [horrible awful tragic sad poor difficult] >> )
	$trust += 1
	I know! This is so unfair! 

s: (poor * [thing you]) Thank you for the compassion.

s:($plot==1 <<[I my] [sympathy sympathize] you>>) That is nice of you.
	$trust += 1

#! ----------------------------------------------------------------
#! Truce tactic
#! ----------------------------------------------------------------

s: ($stepmother==1 I * want * [friendly~2 chat~3]) Somehow I doubt that, given our last three meetings. Whatâ€™s your plan this time?
	threatened += 1 

#! ----------------------------------------------------------------
#! Not being stepmother
#! ----------------------------------------------------------------

s: ($stepmother!=1 ~evilqueen * [dead~1 away~3])
	$trust += 1
	Really!? That's great news!

s: ($stepmother!=1 I * not *[look resemble like] ~evilqueen) Yes but she tends to disguise herself.

s: ($stepmother!=1 I * not * ~evilqueen) How do I know that?

s: ($stepmother!=1 I * [hate~2 despise~1] ~evilqueen) She's not very popular.

s:(<<I not [look resemble resemblance] ~evilqueen>>) True, but she's usually disguised.

s:(<<I not [costume disguise] >>) That easy to say. My stepmother's a magician and can completely change her appearance.

#! ----------------------------------------------------------------
#! Topic related gambits
#! ----------------------------------------------------------------

t: ($plot!=1 $stepmother!=1) I can't be too cautious with my stepmother wanting to kill me.
	$plot = 1
t: ($plot==1 $stepmother!=1) I can't think straight with all this stress.