topic: ~catchall (~emohello ~emohowzit ~emogoodbye ~insults ~damage_verbs really thank thanks~2 ~emothanks)

#! Gambits to fall back on when input doesn't trigger anything more precise

u: ($end==1) The door is shut, no one is hearing you. ^keep() ^repeat()

#! ----------------------------------------------------------------
#! Politeness
#! ----------------------------------------------------------------

u: ([~emohello ~emohowzit hello hi "good morning"]) Hello.
u: ([~emohello ~emohowzit hello hi "good morning"]) You're quite the greeter.
u: ([~emogoodbye bye goodbye ciao "see you"]) Goodbye.
	$end = 1

#! ----------------------------------------------------------------
#! Questions
#! ----------------------------------------------------------------

?: (why) Sorry, why what again?. ^poptopic()
?: (where) I don't know, look at a map. ^poptopic()
?: (how) How what? ^poptopic()
?: (how) In a crafty way, probably. ^poptopic()
?: (who do you think) Err... you?
	a:(~yes) Ok.^poptopic()
	a:(~no) You lost me.^poptopic()
?: (who) Who do you think? ^poptopic()

#! ----------------------------------------------------------------
#! Generic propositions
#! ----------------------------------------------------------------

s:([thanks "thank you" thank thanks~2 ~emothanks])  You're welcome. ^poptopic() ^keep() ^repeat()
s:([oh dear "gosh" totally]) Uh-huh... ^poptopic() ^keep() ^repeat()
s:([maybe]) ... ^poptopic() ^keep() ^repeat()
s: (I * [understand see know]) Uh-huh... ^poptopic() ^keep() ^repeat()
?: ([really "that so" "don't say" incredible]) Yup. ^poptopic() ^keep() ^repeat()
u: (!I [here take have] * ) No thanks. ^poptopic() ^keep() ^repeat()
u: ([nonsense "not making sense" "not making any sense" "mean anything" "means nothing"]) Sorry, I lost my train of thought. What were we talking about? ^poptopic()

#! ----------------------------------------------------------------
#! Threatening and insulting
#! ----------------------------------------------------------------

u: (you [~insults unpleasant_person~1]) You're not being very amiable.
	a:(neither) Sorry about that, it's the anxiety.
	a:(sorry) That's all right.	
u: (you [~insults unpleasant_person~1]) I don't have to listen to this, bye.
	$end = 1
u: (I * [blow~2 hurt kill destroy ~damage_verbs] * you) You're not very credible, old woman.
u: (I * [blow~2 hurt kill destroy ~damage_verbs] * you) Ok, that's it. Bye now!
	$end = 1

#! ----------------------------------------------------------------
#! Impatience gambits
#! ----------------------------------------------------------------

t: ($trust<2) Why are you not leaving me alone? ^poptopic()
t: ($trust>3 $offered) You have some good points but I'm not accepting anything from you. ^poptopic()
t: Not to be rude, but you're abusing your welcome. ^poptopic()
t: I'm going to close this door anytime soon now. ^poptopic()
t: That's it, bye.
	$end = 1

#! ----------------------------------------------------------------
#! Easter Eggs
#! ----------------------------------------------------------------

u: (build 1) Are you trying to compile me again?
u: (reset) You won't get rid of me that easily!


#! Reusable explanation
t: TELLMEWHY () Because strangers have tried to kill me recently. ^addtopic(~assassination_plot)
	$plot = 1
	a:(who) It was actually my stepmother in disguise. ^addtopic(~assassination_plot)
		b:(really) Yes, crazy business.
	a:(how) First it was bodice lace that nearly squeezed the death out of me. Then it was a poisoned comb. ^addtopic(~assassination_plot)