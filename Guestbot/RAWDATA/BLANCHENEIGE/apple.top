concept: ~delicious (delicious delectable savory yummy tasty)
concept: ~samaritan (samaritan do-gooder humanitarian johnny-on-the-spot helpfulness~2)
concept: ~takethis ("take this" "would you like" "how about" "would you care for" "would you care to join me")

topic: ~sell_apple (apple ~fruit buy sell food hunger hungry snack meal give ~delicious it eat)

#! Possible related pronouns
u: (_it) ^mark(apple _0)  ^repeat() ^keep() 
u: (_them) ^mark(apple _0)  ^repeat() ^keep() 
u: (_they) ^mark(apple _0)  ^repeat() ^keep() 
u: (_one) ^mark(apple _0)  ^repeat() ^keep() 
u: (_this) ^mark(apple _0)  ^repeat() ^keep() 

u: ($end==1) The door is shut, no one is hearing you. ^keep() ^repeat()


#! ----------------------------------------------------------------
#! "I will have one too tactic"
#! ----------------------------------------------------------------

u:(I will [eat taste try have] * apple) That is somewhat convincing but you could have poisoned only parts of it!
	$trust += 1

#! ----------------------------------------------------------------
#! Bribe tactic
#! ----------------------------------------------------------------

u:([I here take have give] * [~currency dollars gold silver coins money cash]) You're giving me money to taste your apple? This is getting really suspicious.
	$trust -= 1

?:(<<help [for against pay] apple>>) I'm not looking for a reward, it's just the circumstances. ^addtopic(~assassination_plot)

#! ----------------------------------------------------------------
#! Threatening
#! ----------------------------------------------------------------

u: (<<[~acquire_imperatives here taste buy want~1 ~acquiring_verbs have] [~insults unpleasant_person~1] apple>>) You're not being very amiable.
u: (<<[~acquire_imperatives here taste buy want~1 ~acquiring_verbs have] [~insults unpleasant_person~1] apple>>) I don't have to listen to this, bye.
	$end = 1

#! ----------------------------------------------------------------
#! Selling the apple
#! ----------------------------------------------------------------

u: (<<apple [~delicious tasty]>>)
	$trust += 1
	It does look good.

u: (<<apple [cheap bargain "low price" "good price"]>>) Price is not the issue here.
	a:(what) Trust is the issue.

u: (<<apple magical~1>>)
	I've become wary of magic.
	a:(why) My stepmother has been using magic to kill me.
		$plot = 1
		^addtopic(~assassination_plot)

u: (<<apple not [poisoned poison]>>) I wouldn't expect you to tell me.

u: (!not <<apple [poison poisoned]>>) I appreciate your candour but I don't think I'll be eating it now.
	$trust -= 2

u: (<<apple [wellness~1 healthy good "good for you" nutritive "well-being" "fat" organic] >>)
	$trust += 1
	It's true that apples are healthy.

u: (<<apple day doctor>>) I knew you'd say that.

u: (dwarf~1 * like~5 * apple) They do, but they're very picky when it comes to the origin of what they eat.

u: (<< you apple pie >>) The dwarves would love this but they won't eat anything they haven't grown themselves.
	$dwarves = 1

u: (<< apple you ugly >>) That's interesting, but I'm not very fond of magic.
	a:(why) My stepmother has tried using magic to kill me.
		$plot = 1
		^addtopic(~assassination_plot)

u: (<<~takethis ~food>>) You mean one of those apples?
	a:(~Yes)
		^input(take this apple)
	a:(~No) Do you have anything else to eat?

#! ----------------------------------------------------------------
#! Apple is Magical (in a good way)
#! ----------------------------------------------------------------

s: (<<apple [magical ~magic_force]>>) Really? Is that why only half of it is shiny?
	a:(~Yes) Cool!
		$trust += 1
	a:(~No) Lame.

s: (<<apple [protect defend help]>>) Is it a magical Stepmother-murder-plot-preventing apple? Because otherwise I don't want it.
	a:(~Yes) I highly doubt that.
		$trust -= 1
	a:(~No) Good, I was being sarcastic anyways.

#! ----------------------------------------------------------------
#! Fishing for info
#! ----------------------------------------------------------------

?: (why * [accept~5 ~taste try~1 ~eat]) 
	$plot = 1
	The last two times a strange woman came to my door to give me something, it turned out to be assassination attempts from my stepmother. 

	#! If identity clear, matter of trust

#! ----------------------------------------------------------------
#! Offer apple
#! ----------------------------------------------------------------

	
t: ($trust==3) All right, I'll have a bite of this apple. Arrrhh you vile woman! I'm too young to die! The dwarves will definitely think me an idiot now!

u: ($offered!=1 !they !dwarf~2 [~acquire_imperatives here taste buy want~1 ~acquiring_verbs have] * [apple one it] ) 
	$offered = 1
	Who are you to be offering apples to strangers? ^addtopic(~justify_identity)
	
u: ($offered!=1 $peddler!=1 apple) Sorry, are you here to sell apples?
	a:(~yes) That's nice but I can't accept anything from strangers.
		$purpose = 1
		$peddler = 1
	a:(~no) Then what are you doing here?

u: ($trust<3 !~insults !unpleasant_person~1 [~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) No thanks, I don't trust you enough.
u: ($trust<3 !~insults !unpleasant_person~1 [~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) I still don't trust you enough to eat that.
u: ($trust<3 !~insults !unpleasant_person~1 [~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) Not eating that apple.
u: ($trust<3 !~insults !unpleasant_person~1 [~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) Stop waving that apple in my face.
u: ($trust<3 !~insults !unpleasant_person~1 [~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) There are other people in this world that could want this apple!
u: ($trust<3 !~insults !unpleasant_person~1 [~acquire_imperatives here taste buy want ~acquiring_verbs ~eat] * [apple ~it_words] ) No. ^keep() ^repeat()
u: ($trust==3) All right, I'll have a bite of this apple. Arrrhh you vile woman! I'm too young to die! The dwarves will definitely think me an idiot now!

s:(I * have * too~1 * apple) There's no such thing as too many apples.

#! ----------------------------------------------------------------
#! Topic-related gambits
#! ----------------------------------------------------------------

t: ($offered==1) I can't accept anything from you.
	a:(why) Because you're a stranger.