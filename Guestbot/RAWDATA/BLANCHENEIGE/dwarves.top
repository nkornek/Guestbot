topic: ~dwarves (dwarf~2 ~mythical_intelligent_being dwarves)

#! Possible related pronouns
u: (_they) ^mark(dwarf _0)  ^repeat() ^keep() 
u: (_them) ^mark(dwarf _0)  ^repeat() ^keep() 

u: ($end==1) The door is shut, no one is hearing you. ^keep() ^repeat()

#! ----------------------------------------------------------------
#! Undermine dwarves tactic
#! ----------------------------------------------------------------

s: ([they dwarf~2] * [keep prison~1 ~jail kidnap~1 confine~2]) 
	You think so? ... they seem so nice.
	$trust += 1

s: ([they dwarf~2] * stupid~1)
	$trust += 1
	Yeah, I guess they are kinda dumb. Can you believe that they thought the earth was round?

s: ([they dwarf~2] * unpleasant_person~1)
	$trust -= 1
	Don't talk like that about these generous creatures!

s: (~acquiring_verbs * ~fruit * [they dwarf~2])
	$trust -= 1
	That seems like an awful lot for a merchant to just give away.

u: ( << you [dwarf~2] [food~1 apple] [give feed] >>)
	We mostly eat cabbages. I admit I'm a bit tired of that.
	$trust += 1


u: (<< [normal natural suitable fitting moral think virtue virtuous proper] live with dwarf~2>>) Is it wrong for a young girl to live with seven grown--albeit small--men?
	a:(~no) Oh dear, I had never thought of this that way.
		$trust += 1
	a:(~yes) It does feel right.
		$trust += 1

#! ----------------------------------------------------------------
#! Praise dwarves tactic
#! ----------------------------------------------------------------

s: ([they dwarf~2] * ~goodness) Yes they've been very good to me
	$trust += 1

#! ----------------------------------------------------------------
#! Relation of the dwarves tactic
#! ----------------------------------------------------------------

s: ( << friend~1 dwarf~2 I >>) They never mentioned you.

?: ( << I dwarf~2 [meet see appointment rendez-vous] >>) Come back when they're there.
	a:(when) At dusk.

?: (<< dwarf~2 be [home house inside here available] >>) This is their house but they're out at the mountain.
	$dwarves = 1

s: (<< apple dwarf~2 [bring deliver sell for] >>) The dwarves didn't tell me about you, come back when they're there.
	a:(when) At dusk.

#! ----------------------------------------------------------------
#! Dwarven approval
#! ----------------------------------------------------------------

s:(dwarf * [apple] * [ok good accept acceptable "good for you"]) Well maybe if the dwarves said so...
	$trust += 1

#! ----------------------------------------------------------------
#! Fishing for info
#! ----------------------------------------------------------------

?: (who * be * dwarf~2) I live with seven little men.
	$dwarves=1
	a:(why) They've been very nice to me.

?: (where * be * dwarf~2) Digging the mountain for ore.

?: (when * dwarf~2) They will come back at dusk.

?: (<<[how like] dwarf~2 live>>) It's a bit strange living with seven men half my size, but you get used to it.

?: (<<be dwarf~2 [friendly nice pleasant affable amiable attentive helpful loving sympathetic kind tender fond]>>) Yes, they're great.

u: ( <<  [dwarf~2] [food~1 apple] [like enjoy eat] >>)
	I guess the dwarves could like apples but they won't eat anything coming from a stranger.
	a: (why) They're a bit paranoid.

?: (<< I wait [inside house] >>) The dwarves forbid me to let anyone it. At the end of the road you'll find a small inn.

#! ----------------------------------------------------------------
#! Topic-related Gambits
#! ----------------------------------------------------------------

t: The dwarves told me not to let anyone in.
	a:([smart good clever]) Yes, they are nobody's fools.
	a:(why) ^reuse(justify_identity.TELLMEWHY)
t: ($dwarves==1) The dwarves would think I'm really stupid if I accept another gift from a stranger.