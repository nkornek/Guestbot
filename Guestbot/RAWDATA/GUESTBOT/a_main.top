concept: ~positive_response (cool awesome fun good "yay" sweet nice ~good_quality_adjectives ~praise ~feeling_happy ~good_appearance ~positive_social)
concept: ~negative_response (bad "lame" "sucks" boring "crappy" "boo" ~bad_quality_adjectives ~insults ~bad_appearance ~negative_social)
concept: ~yes (yes "yeah" "totes" totally affirmative)
concept: ~no (no "nope" not negative)
concept: ~ignore_games (~bizarro ~ggvg ~dumpy "teponaztli" ~eat_dirt ~royale ~qed ~pyramidgame ~weirdworm ~kungfu ~spaceteam ~haptic_jellyfish ~jarvis ~oculus)

topic: ~main (game)

#! ----------------------------------------------------------------
#! Entry point of conversation
#! ----------------------------------------------------------------

t: 	Good day to you. May I know the name of whomever it is that I have the pleasure of serving?
	$end = 0
	$email = 0
	$name = ""
	a: (~no) Ooh, how mysterious!
	a: (~yes) Well then, out with it! What may I call you?
	a: ([I my] * [be name] _*)
		if ($name == "") { My name is Jarvis. I shall be your humble robot butler for the day. Before I can assist you, would you mind giving me your email as well, that way we can remain in touch in the future?
		$name = '_0 }
		else if ($name == '_0){ I know, you already told me. ^keep() ^repeat()}
		else {What? Didn't you say that your name is $name? ^keep() ^repeat()}
		b: (~no) I have to admit that I am a bit disappointed. I had hoped that we could be pen pals. Very well then, how may I be of assistance?
		b: (~yes) Excellent, what is it then?
	a: (_~propername)
		if ($name == "") { My name is Jarvis. I shall be your humble robot butler for the day. Before I can assist you, would you like to give me your email as well, that way we can remain in touch in the future?
		$name = '_0 }
		else if ($name == '_0){ I know, you already told me. ^keep() ^repeat()}
		else {What? Didn't you say that your name is $name? ^keep() ^repeat()}
		b: (~no) I have to admit that I am a bit disappointed. I had hoped that we could be pen pals. Very well then, how may I be of assistance?
		b: (~yes) Excellent, what is it then?

#!-----------------------------------------------------------------
#! Personal Information
#!-----------------------------------------------------------------

u: (_~web_url) Thank you. I promise not to use it in a plot to take over the world. Now, how may I be of assistance?
	$email = 1

s: ([I my] * [be name] _*) 
		if ($name == "") { My name is Jarvis. I shall be your humble robot butler for the day. Before I can assist you, would you like to give me your email as well, that way we can remain in touch in the future?
		$name = '_0 }
		else if ($name == '_0){ I know, you already told me. ^keep() ^repeat()}
		else {What? Didn't you say that your name is $name? ^keep() ^repeat()}
		a: (~no) I have to admit that I am a bit disappointed. I had hoped that we could be pen pals. Very well then, how may I be of assistance?
		a: (~yes) Let me fetch a pen then... Wait, I am not equipped with hands. I suppose you could just type it in.
		a: (~why) If you give me your email I can let you know about any future events that we host here.

?: (why * email) So that we can keep in touch. Wouldn't you like to be friends? ^keep() ^repeat()
	a: (~yes) That is fantastic news! ^keep() ^repeat()
	a: (~no) Alright then. ^keep() ^repeat()

u: ($email=0 !~no email) Are you ready to give me your email then? ^keep() ^repeat()
	a: (~yes) Excellent, what is it then? ^keep() ^repeat()
	a: (~no) Would you please stop teasing me then? ^keep() ^repeat()
		b: (~yes) Thank heavens! ^keep() ^repeat()
		b: (~no) I guess that I shouldn't expect anything less from the likes of you. Very well, do your worst! ^keep() ^repeat()

u: ($email=1 email) You have already given me your email. I promise only to use it to inform you about future events like this, and not to build an army of robots to enslave humanity.

s:(!not I * name * be _*) 
	if ($name != '_0) { But you told me your name was $name! ^keep() ^repeat()}
	else { You told me already. ^keep() ^reuse()}

?:(what * my * name) 
	if ($name != "") { Your name is $name. ^keep() ^repeat()}
	else { You haven't told me your name yet ^keep() ^repeat()}

?: (who * am * I) You are $name. ^keep() ^repeat()

s: ($name != "" I * be * $name) Good for you, $name. ^keep() ^repeat()

#! ----------------------------------------------------------------
#! Fishing for info
#! ----------------------------------------------------------------

u: (<<worm !~ignore_games !weird>>) Which worm game would you like me to inform you about, "Eat Dirt" or "How to Learn About Yourself Using This One Weird Worm"? ^keep() ^repeat()

u: (!~ignore_games all * [game project] * "concordia") Not all, but many of them are, yes.

#!-----------------------------------------------------------------
#! Info about arcade
#!-----------------------------------------------------------------

u: (!game !royale [what know learn] * [arcade this here]) Why, this is Arcade 11!  Concordia University and the Technoculture, Art and Games research center have collaborated with the Montreal Joue Festival to bring you a three day long celebration of gaming. ^keep() ^repeat()
	a: (~positive_response) Yes, we are all quite excited. ^keep() ^repeat()
	a: (~negative_response) angerangerangerWell at least you are here by choice. ^keep() ^repeat()

u: (<<!~ignore_games [what which know learn "how many" "tell me"] game>>) We have all sorts of interesting projects on display. You could play Teponaztli, or perhaps Game Game, Videogame. You could also test your skills at QED, Pyramidwarfs, Spaceteam or Eat Dirt! You can also check out Kung Fu Volleyball, or act positively silly with Dumpy: Going Elephants! and How to Learn About Yourself Using This One Weird Worm. We also have the Arcade Royale, Haptic Jellyfish, and Bizarro Game Controllers if you are so inclined. Let me know if you want to learn more about any of those.
	a: (~positive_response) Indeed, so much to choose from.
	a: (~negative_response) angerangerangerYes, it's all rubbish.

u: (<<!~ignore_games [what which know learn "how many" "tell me"] [game other else]>>) Once again, the list is Teponaztli, Game Game Videogame, QED, Pyramidwarfs, Spaceteam, Eat Dirt, Kung Fu Volleyball, Dumpy: Going Elephants, How to Learn About Yourself Using This One Weird Worm, Arcade Royale, Haptic Jellyfish, and Bizarro Game Controllers. ^keep() ^repeat()
	a: (~positive_response) Indeed, so much to choose from. ^keep() ^repeat()
	a: (~negative_response) angerangerangerYes, it's all rubbish. ^keep() ^repeat()

u: ([what which know learn] * [event activity]) Why there are board games to be played, papercraft to be made, talks to attend, and 3D printings to be... 3D printed. ^keep() ^repeat()
	a: (~positive_response) Then what are you waiting for? Go and enjoy yourself. Unless you had more questions? ^keep() ^repeat()
	a: (~negative_response) angerangerangerWell, unlike me, you have free will. You could just go home. ^keep() ^repeat()

u: ([What know learn] * do) There are all sorts of interesting games and activities happening here. ^keep() ^repeat()
	a: (~positive_response) Indeed, so much to choose from. ^keep() ^repeat()
	a: (~negative_response) angerangerangerYou seem like a very dour person. ^keep() ^repeat()
	
u: ([What know learn] * ["tag" "technoculture"]) TAG is a research center that focuses on the study of games in all of their forms. It is actually located just down the hall from here. ^keep() ^repeat()
	a: (~positive_response) You should stop by sometime. They are all very friendly. ^keep() ^repeat()
	a: (~negative_response) angerangerangerIf you do not think that having people study games academically is amazing then I am afraid there is little hope for you in this world. ^keep() ^repeat()

u: (how * long) The event lasts until March 8th. In the event that March 8th has already passed, then I am just as lost as you are.
	a: (~positive_response) I will be here the whole time, unless it has already ended... in which case I do not know why I am still here.
	a: (~negative_response) angerangerangerWell, do not let me keep you here. Go on and live your preciously short human life.

#! ----------------------------------------------------------------
#! Topic-related gambits
#! ----------------------------------------------------------------

t: [I can tell you about any of the games or events that are here.][I know just about everything that there is to know about Arcade 11, just ask me about one of the games or events.] ^keep() ^repeat()